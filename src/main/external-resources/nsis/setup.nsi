!include "MUI.nsh"
!include "FileFunc.nsh"
!include "TextFunc.nsh"
!include "WordFunc.nsh"
!include "LogicLib.nsh"
!include "nsDialogs.nsh"

; Include the project header file generated by the nsis-maven-plugin
!include "..\..\..\..\target\project.nsh"
!include "..\..\..\..\target\extra.nsh"

!define REG_KEY_UNINSTALL "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${PROJECT_NAME}"
!define REG_KEY_SOFTWARE "SOFTWARE\${PROJECT_NAME}"

RequestExecutionLevel admin

Name "PS3MediaServer"
InstallDir "$PROGRAMFILES\${PROJECT_NAME}"

;Get install folder from registry for updates
InstallDirRegKey HKCU "${REG_KEY_SOFTWARE}" ""

SetCompressor /SOLID lzma
SetCompressorDictSize 32

!define MUI_ABORTWARNING
!define MUI_FINISHPAGE_RUN "$INSTDIR\PMS.exe"
!define MUI_WELCOMEFINISHPAGE_BITMAP "${NSISDIR}\Contrib\Graphics\Wizard\win.bmp"

!define MUI_FINISHPAGE_SHOWREADME ""
!define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
!define MUI_FINISHPAGE_SHOWREADME_TEXT "Create Desktop Shortcut"
!define MUI_FINISHPAGE_SHOWREADME_FUNCTION CreateDesktopShortcut

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_DIRECTORY
Page custom SetMem SetMemLeave ;Custom page
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_LANGUAGE "English"

; Defins for perl
!define PERL_URL "http://downloads.activestate.com/ActivePerl/releases/5.14.2.1402/ActivePerl-5.14.2.1402-MSWin32-x86-295342.msi"


ShowUninstDetails show

Var Dialog
Var Text
Var Label

Function SetMem
	nsDialogs::Create 1018
	Pop $Dialog

	${If} $Dialog == error
		Abort
	${EndIf}
	
	${NSD_CreateLabel} 0 0 50% 12u "Enter amount of memory to give to PMS"
	Pop $Label

	${NSD_CreateText} 50% 0 50% 12u "768"
	Pop $Text

	nsDialogs::Show
FunctionEnd

Function SetMemLeave
	${NSD_GetText} $Text $0
	WriteRegStr HKCU "${REG_KEY_SOFTWARE}" "HeapMem" "$0"
FunctionEnd

Function CreateDesktopShortcut
  CreateShortCut "$DESKTOP\${PROJECT_NAME}.lnk" "$INSTDIR\PMS.exe"
FunctionEnd

Section "Program Files"
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File /r /x "*.lin" /x "*.conf" /x "*.zip" /x "*.dll" /x "third-party" "${PROJECT_BASEDIR}\src\main\external-resources\plugins"
  File /r "${PROJECT_BASEDIR}\src\main\external-resources\documentation"
  File /r "${PROJECT_BASEDIR}\src\main\external-resources\renderers"
  File /r /x "*.lin" "${PROJECT_BASEDIR}\src\main\external-resources\extras"
  ;File /r "${PROJECT_BINARIES}\win32"
  ;File /r "${PROJECT_BINARIES}\plugins"
  ;File /r "${PROJECT_BINARIES}\extras\bin"
  File /r "${PROJECT_BASEDIR}\target\bin\win32"
  File /r "${PROJECT_BASEDIR}\target\bin\"
  File "${PROJECT_BUILD_DIR}\PMS.exe"
  File "${PROJECT_BASEDIR}\src\main\external-resources\PMS.bat"
  File "${PROJECT_BASEDIR}\src\main\external-resources\PMS.conf.new"
  File "${PROJECT_BASEDIR}\src\main\external-resources\PMS.cred.new"
  File "${PROJECT_BUILD_DIR}\pms.jar"
  ;File "${PROJECT_BINARIES}\MediaInfo.dll"
  ;File "${PROJECT_BINARIES}\MediaInfo64.dll"
  File /r "${PROJECT_BASEDIR}\target\bin\MediaInfo.dll"
  File /r "${PROJECT_BASEDIR}\target\bin\MediaInfo64.dll"
  File "${PROJECT_BASEDIR}\CHANGELOG"
  File "${PROJECT_BASEDIR}\README.md"
  File "${PROJECT_BASEDIR}\LICENSE.txt"
  File "${PROJECT_BASEDIR}\src\main\external-resources\logback.xml"
  File "${PROJECT_BASEDIR}\src\main\external-resources\icon.ico"
  File "${PROJECT_BASEDIR}\src\main\external-resources\MOVIEINFO.conf"
  

  ;the user may have set the installation dir
  ;as the profile dir, so we can't clobber this
  SetOverwrite off
  File "${PROJECT_BASEDIR}\src\main\external-resources\WEB.conf"

  ;Store install folder
  WriteRegStr HKCU "${REG_KEY_SOFTWARE}" "" $INSTDIR

  ;Create uninstaller
  WriteUninstaller "$INSTDIR\uninst.exe"

  WriteRegStr HKEY_LOCAL_MACHINE "${REG_KEY_UNINSTALL}" "DisplayName" "${PROJECT_NAME}"
  WriteRegStr HKEY_LOCAL_MACHINE "${REG_KEY_UNINSTALL}" "DisplayIcon" "$INSTDIR\icon.ico"
  WriteRegStr HKEY_LOCAL_MACHINE "${REG_KEY_UNINSTALL}" "DisplayVersion" "${PROJECT_VERSION}"
  WriteRegStr HKEY_LOCAL_MACHINE "${REG_KEY_UNINSTALL}" "Publisher" "${PROJECT_ORGANIZATION_NAME}"
  WriteRegStr HKEY_LOCAL_MACHINE "${REG_KEY_UNINSTALL}" "URLInfoAbout" "${PROJECT_ORGANIZATION_URL}"
  WriteRegStr HKEY_LOCAL_MACHINE "${REG_KEY_UNINSTALL}" "UninstallString" '"$INSTDIR\uninst.exe"'

  ${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
  IntFmt $0 "0x%08X" $0
  WriteRegDWORD HKLM "${REG_KEY_UNINSTALL}" "EstimatedSize" "$0"

  WriteUnInstaller "uninst.exe"

  ReadENVStr $R0 ALLUSERSPROFILE
  SetOutPath "$R0\PMS-SHB"
  AccessControl::GrantOnFile "$R0\PMS-SHB" "(S-1-5-32-545)" "FullAccess"
  ;File "${PROJECT_BASEDIR}\src\main\external-resources\PMS.conf.new"
  ;File "${PROJECT_BASEDIR}\src\main\external-resources\PMS.cred.new"
  ; Append all PATHs to PMS.conf.new
  ;${WordReplace} "$INSTDIR\extras" "\" "\\" "+" $R0
  ${WordReplace} "extras" "\" "\\" "+" $R0
  ${WordReplace} "$OUTDIR" "\" "\\" "+" $R3
  ; Channels
  ${ConfigWrite} "PMS.conf.new" "channels.path=" "$R0\\channels" $R2
  ; PMS encoder
  ${ConfigWrite} "PMS.conf.new" "pmsencoder.script.directory = " "$R0\\scripts" $R2
  ; DbgPack
  ${WordReplace} "pmsencoder.log" "\" "\\" "+" $R1 
  ${ConfigWrite} "PMS.conf.new" "dbgpack = " "$R0\\channels\\channel.log,$R1" $R2
  ; Downloader scripts
  ${ConfigWrite} "PMS.conf.new" "youtube-dl.path = " "$R0\\bin\\youtube-dl.exe" $R2
  ${ConfigWrite} "PMS.conf.new" "get-flash-videos.path = " "$R0\\scripts\\get_flash_videos.pl" $R2
  ; Perl 
  ${ConfigWrite} "PMS.conf.new" "perl.path = " "$R0\\perl\\bin\\perl.exe" $R2
  ; Rtmpdump
  ${ConfigWrite} "PMS.conf.new" "rtmpdump.path = " "$R0\\bin\\rtmpdump.exe" $R2
  ; Curl and cookie
  ${ConfigWrite} "PMS.conf.new" "curl.path = " "$R0\\bin\\curl.exe" $R2
  ${ConfigWrite} "PMS.conf.new" "cookie.path = " "$R0\\cookies" $R2
  ; Cred path
  ${ConfigWrite} "PMS.conf.new" "cred.path = " "$R3\\PMS.cred" $R2
  ; Verify if PMS.conf exists, if not move .new to PMS.conf
  ;${GetFileAttributes} "PMS.conf" "ALL" $R2
  ;IfErrors 0 +2
  ;CopyFiles "PMS.conf.new" "PMS.conf"
SectionEnd

Section "Start Menu Shortcuts"
  SetShellVarContext all
  CreateDirectory "$SMPROGRAMS\${PROJECT_NAME}"
  CreateShortCut "$SMPROGRAMS\${PROJECT_NAME}\${PROJECT_NAME}.lnk" "$INSTDIR\PMS.exe" "" "$INSTDIR\PMS.exe" 0
  CreateShortCut "$SMPROGRAMS\${PROJECT_NAME}\Uninstall.lnk" "$INSTDIR\uninst.exe" "" "$INSTDIR\uninst.exe" 0
SectionEnd

Section "Desktop shortcut"
  SetShellVarContext all
  CreateShortCut "$DESKTOP\PS3 Media Server-SHB.lnk" "$INSTDIR\pms.exe"
SectionEnd

Section "Uninstall"
  SetShellVarContext all

  Delete /REBOOTOK "$INSTDIR\uninst.exe"
  RMDir /R /REBOOTOK "$INSTDIR\plugins"
  RMDir /R /REBOOTOK "$INSTDIR\renderers"
  RMDir /R /REBOOTOK "$INSTDIR\documentation"
  RMDir /R /REBOOTOK "$INSTDIR\win32"
  RMDir /R /REBOOTOK "$INSTDIR\extras"
  Delete /REBOOTOK "$INSTDIR\PMS.exe"
  Delete /REBOOTOK "$INSTDIR\PMS.bat"
  Delete /REBOOTOK "$INSTDIR\pms.jar"
  Delete /REBOOTOK "$INSTDIR\MediaInfo.dll"
  Delete /REBOOTOK "$INSTDIR\MediaInfo64.dll"
  Delete /REBOOTOK "$INSTDIR\CHANGELOG"
  Delete /REBOOTOK "$INSTDIR\WEB.conf"
  Delete /REBOOTOK "$INSTDIR\README.md"
  Delete /REBOOTOK "$INSTDIR\LICENSE.txt"
  Delete /REBOOTOK "$INSTDIR\debug.log"
  Delete /REBOOTOK "$INSTDIR\logback.xml"
  Delete /REBOOTOK "$INSTDIR\icon.ico"
  RMDir /REBOOTOK "$INSTDIR"

  Delete /REBOOTOK "$DESKTOP\${PROJECT_NAME}.lnk"
  RMDir /REBOOTOK "$SMPROGRAMS\${PROJECT_NAME}"
  Delete /REBOOTOK "$SMPROGRAMS\${PROJECT_NAME}\${PROJECT_NAME}.lnk"
  Delete /REBOOTOK "$SMPROGRAMS\${PROJECT_NAME}\Uninstall.lnk"

  DeleteRegKey HKEY_LOCAL_MACHINE "${REG_KEY_UNINSTALL}"
  DeleteRegKey HKCU "${REG_KEY_SOFTWARE}"

  nsSCM::Stop "${PROJECT_NAME}"
  nsSCM::Remove "${PROJECT_NAME}"
SectionEnd
